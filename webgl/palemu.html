<html><head><title>PAL-emulation experiment</title><script id="shader-fs" type="x-shader/x-fragment"> 
#ifdef GL_ES
precision highp float;
#endif

/* Coordinates being currently rendered. */
varying vec2 vPosition;
uniform sampler2D uScreen;
uniform sampler2D uPalette;
uniform float uOutputHeight;

const float REFERENCE_PAL_GAMMA = 2.8;
const float DISPLAY_SYSTEM_GAMMA = 2.0;
const float BRIGHTNESS = -0.03;
const float CONTRAST = 0.8;
const float SATURATION = 0.5;
const float PI = 3.14159265358979323846264338327950288419716939937510;

/* Screen start and end coordinates. */
const vec2 SCREENTOPLEFT = vec2(0., 0.);
const vec2 SCREENSIZE = vec2(256., 256.);

/* Conversion to RGB */
const mat3 YUV2RGB = mat3(
        1.     ,  1.     , 1.     ,
        0.     , -0.39465, 2.03211,
        1.13983, -0.58060, 0.
);

const float YL = 0.25;
const float YH = 0.5;

/* Convert C64 palette index at given position to YUV color. */
vec3 getPixel(ivec2 position) {
    /* Yields color index 0 .. 15 */
    float index = texture2D(uScreen, (vec2(position) + .5) / 512.).x * 256.;

    /* Select odd/even palette. */
    float row = .51 - fract(float(position.y) * .5) * .25;

    /* Lookup vec2(color, rotation) palette index. */
    vec3 yuv = texture2D(uPalette, vec2((index + .5) / 16., row)).rgb;

    /* Normalize */
    return yuv - vec3(0., .5, .5);
}

/* Simple approximation of horizontal bandwidth at y, u and v. */
vec3 getHorizontallyFilteredPixel(ivec2 position) {
    vec3 yuv0 = getPixel(position + ivec2(-1, 0.));
    vec3 yuv1 = getPixel(position               );
    vec3 yuv2 = getPixel(position + ivec2( 1, 0.));

    return vec3(
        (yuv0.r + yuv2.r) * YL + yuv1.r * YH,
        (yuv0.gb + yuv1.gb + yuv2.gb) * .33 * SATURATION
    );
}

/* PAL alternating lines UV average,
 * RGB output with physical intensity */
vec3 getFilteredPixel(ivec2 position) {
    vec3 yuvBefore = getHorizontallyFilteredPixel(position + ivec2(0, -1));
    vec3 yuvCurrent = getHorizontallyFilteredPixel(position);
    vec3 yuv = vec3(yuvCurrent.r, .5 * (yuvBefore.gb + yuvCurrent.gb));
    vec3 rgb = YUV2RGB * yuv;

    return vec3(
        pow(rgb.x, REFERENCE_PAL_GAMMA),
        pow(rgb.y, REFERENCE_PAL_GAMMA),
        pow(rgb.z, REFERENCE_PAL_GAMMA)
    );
}

/* Brightness, contrast, gamma. */
vec3 applyPCMonitorParams(vec3 rgb) {
    rgb = (rgb + BRIGHTNESS) * CONTRAST;
    return vec3(
        pow(rgb.r, 1./DISPLAY_SYSTEM_GAMMA),
        pow(rgb.g, 1./DISPLAY_SYSTEM_GAMMA),
        pow(rgb.b, 1./DISPLAY_SYSTEM_GAMMA)
    );
}

/* Scanline function is .9 + .1 * cos(2 * pi * y) */
float scanlineIntegral(in float x) {
    return .9 * x + .1 * cos(2. * PI * x) / (2. * PI);
}

/* Generate the mask to simulate scanlines. */
float scanlineFilter(in float row, in float offset) {
    float i1 = scanlineIntegral(row - offset);
    float i2 = scanlineIntegral(row + offset);
    return (i2 - i1) / offset * .5;
}

void main(void) {
    /* Position in c64-screen texture coords */
    vec2 fpos = (vPosition * vec2(.5, -.5) + .5) * SCREENSIZE + SCREENTOPLEFT;

    /* Offset by 1/2 units to avoid 2 pixel high top during interpolation. */
    float offset = SCREENSIZE.y / uOutputHeight * .5;
    fpos.y += offset;
    ivec2 ipos = ivec2(fpos);

    /* The line immediately before current line on display screen. */
    vec3 rgbBefore = getFilteredPixel(ipos + ivec2(0, -1));
    /* The current line. */
    vec3 rgbCurrent = getFilteredPixel(ipos);

    /* Linear interpolation and scanlines. */
    vec3 rgb = mix(rgbBefore, rgbCurrent, fract(fpos.y));
    rgb *= scanlineFilter(fract(fpos.y - offset), offset);

    /* Brightness, contrast, gamma. */
    rgb = applyPCMonitorParams(rgb);

    /* Render. */
    gl_FragColor = vec4(rgb, 1.);
}
</script> 

<script id="shader-vs" type="x-shader/x-vertex"> 
attribute vec3 aVertexPosition;

varying vec2 vPosition;

void main(void) {
    gl_Position = vec4(aVertexPosition, 1.0);
    vPosition = gl_Position.xy;
}
</script> 

<script type="text/javascript" src="webgl.js"></script>
<script type="text/javascript">
function generatePalette(gl) {
    var lum = [
         540., 1850.,  900., 1340.,
         980., 1150.,  810., 1520.,
         980.,  810., 1150.,  900.,
        1110., 1520., 1110., 1340.
    ];

    var ANGLE_RED =  112.5 / 180.*Math.PI;
    var ANGLE_GRN = -135.0 / 180.*Math.PI;
    var ANGLE_BLU =    0.0 / 180.*Math.PI;
    var ANGLE_ORN =  -45.0 / 180.*Math.PI;
    var ANGLE_BRN =  157.5 / 180.*Math.PI;

    var c64Palette = [
        [ lum[0], ANGLE_ORN, -0],
        [ lum[1], ANGLE_BRN,  0],
        [ lum[2], ANGLE_RED,  1],
        [ lum[3], ANGLE_RED, -1],
        [ lum[4], ANGLE_GRN, -1],
        [ lum[5], ANGLE_GRN,  1],
        [ lum[6], ANGLE_BLU,  1],
        [ lum[7], ANGLE_BLU, -1],
        [ lum[8], ANGLE_ORN, -1],
        [ lum[9], ANGLE_BRN,  1],
        [lum[10], ANGLE_RED,  1],
        [lum[11], ANGLE_RED, -0],
        [lum[12], ANGLE_GRN, -0],
        [lum[13], ANGLE_GRN,  1],
        [lum[14], ANGLE_BLU,  1],
        [lum[15], ANGLE_BLU, -0]
    ];
    var maxLum = c64Palette[1][0];

    var pixels = new Uint8Array(16 * 16 * 4);
    var index = 0;
    for (var y = 0; y < 16; y ++) {
        var angle = (y - 8.) / 16 * Math.PI * .5;
        for (var x = 0; x < 16; x ++) {
            var p = c64Palette[x];

            var u = p[2] * Math.cos(p[1] + angle) * .5;
            var v = p[2] * Math.sin(p[1] + angle) * .5;

            pixels[index ++] = Math.round(p[0] / maxLum * 255);
            pixels[index ++] = Math.round((u + .5) * 255);
            pixels[index ++] = Math.round((v + .5) * 255);
            index ++;
        }
    }

    var texture = gl.createTexture();
    gl.pixelStorei(gl.UNPACK_ALIGNMENT, 1);
    gl.pixelStorei(gl.UNPACK_FLIP_Y_WEBGL, true);
    gl.bindTexture(gl.TEXTURE_2D, texture);
    gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, 16, 16, 0, gl.RGBA, gl.UNSIGNED_BYTE, pixels);
    gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.NEAREST);
    gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.NEAREST);
    return texture;
}

function generateC64Screen(gl) {
    var pixels = new Uint8Array(512 * 512 * 4);
    var index = 0;
    for (var y = 0; y < 512; y ++) {
        for (var x = 0; x < 512; x ++) {
             pixels[index] = (y & 1) == 0 ? y >> 4 : x >> 4;
             index += 4;
        }
    }

    var texture = gl.createTexture();
    gl.pixelStorei(gl.UNPACK_ALIGNMENT, 1);
    gl.pixelStorei(gl.UNPACK_FLIP_Y_WEBGL, false);
    gl.bindTexture(gl.TEXTURE_2D, texture);
    gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, 512, 512, 0, gl.RGBA, gl.UNSIGNED_BYTE, pixels);
    gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.NEAREST);
    gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.NEAREST);

    return texture;
}

function effectInit(gl, shader) {
    var paletteTexture = generatePalette(gl);
    var screenTexture = generateC64Screen(gl);
    gl.activeTexture(gl.TEXTURE0);
    gl.bindTexture(gl.TEXTURE_2D, screenTexture);
    gl.uniform1i(gl.getUniformLocation(shader, "uScreen"), 0);
    gl.activeTexture(gl.TEXTURE1);
    gl.bindTexture(gl.TEXTURE_2D, paletteTexture);
    gl.uniform1i(gl.getUniformLocation(shader, "uPalette"), 1);
   
    var canvas = document.getElementById("canvas"); 
    gl.uniform1f(gl.getUniformLocation(shader, "uOutputHeight"), canvas.height);
    effectStart(gl, shader);
}
</script>
</head>
<body>

<h1>PAL experiment</h1>
<canvas id="canvas" width="512" height="512"></canvas>

</body>
</html>
