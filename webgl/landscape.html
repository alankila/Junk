<html><head><title>Landscape rendering experiment</title><script id="shader-fs" type="x-shader/x-fragment"> 
#ifdef GL_ES
precision highp float;
#endif

/* System's clock, for running animations. */
uniform float uTime;
/* Position of camera. */
uniform vec3 uPos;
/* Lookat of camera. */
uniform vec3 uDir;
/* Water texture */
uniform sampler2D uWatermap;
/* Noise texture */
uniform sampler2D uNoise;

/* Coordinates being currently rendered. */
varying vec2 vPosition;

/* How far to subdivide modified Perlin noise.
 * (Determines how coarse the terrain is.) */
const int MAPITERATIONS = 8;
/* How many march iterations per ray. */
const int RAYITERATIONS = 256;
/* Convert distance between ray and terrain position to "safe" advancing step. */
const float MARCH_SPEED = 0.35;
/* Level Of Detail: how far to render until dropping terrain features. */
const float LOD = 2.0;
/* When ray reaches close enough to terrain, stop marching */
const float STOPCONDITION = 0.0025;
/* Render distance. */
const float FOG = 4.;
/* Direction to infinitely far-away Sun */
const vec3 SUN = vec3(0.8, 0., 0.5);
/* The camera up direction. */
const vec3 UP = vec3(0., 0., 1.);

/* Substitute with white noise generator. */
float getNoise(in float x, in float y) {
    return texture2D(uNoise, vec2(x, y)).b;
}

/* Derivation by Inigo Quilez. */
vec3 getInterpolatedNoise(in vec2 pos) {
    float i = floor(pos.x) / 256.;
    float j = floor(pos.y) / 256.;
    float u = fract(pos.x);
    float v = fract(pos.y);

    /*
    float du = -30.0*u*u*(u*(u-2.0)+1.0);
    float dv = -30.0*v*v*(v*(v-2.0)+1.0);
    u = u*u*u*(u*(u*6.0-15.0)+10.0);
    v = v*v*v*(v*(v*6.0-15.0)+10.0);
    */

    /* Inigo claims that there could be discontinuity artifacts with these
     * equations rather than the set above. Works for me... */
    float du = -6. * u * (1. - u);
    float dv = -6. * v * (1. - v);
    u = u * u * (3. - 2. * u);
    v = v * v * (3. - 2. * v);

    float a = getNoise(i, j);
    float b = getNoise(i+1./256., j);
    float c = getNoise(i, j+1./256.);
    float d = getNoise(i+1./256., j+1./256.);

    float k0 = a;
    float k1 = b - a;
    float k2 = c - a;
    float k3 = d - c - k1;

    return vec3(
        k0 + k1*u + k2*v + k3*u*v,
        du * (k1 + k3*v),
        dv * (k2 + k3*u)
    );
}

/* Generate Perlin noise to given octave. */
float getPerlin(in vec2 pos, in int maxLevel) {
    float o = 0.;
    float w = 0.5;
    for (int i = 0; i < MAPITERATIONS; i ++) {
        vec3 n = getInterpolatedNoise(pos);
        o += w * n.x;
        if (i >= maxLevel) {
            break;
        }
        w *= .5;
        pos *= 2.;
    }
    return o;
}

/* Modified Perlin noise according to Inigo Quilez. */
float getHeight(in vec2 pos, in int maxLevel) {
    float o = 0.;
    float w = 1.0;
    vec2 d = vec2(0., 0.);
    for (int i = 0; i < MAPITERATIONS; i ++) {
        vec3 n = getInterpolatedNoise(pos);
	d += n.yz;
        o += w * n.x / (1. + dot(d, d));
        if (i >= maxLevel) {
            break;
        }
        w *= .5;
        pos *= 2.;
    }
    return o;
}

/* Modified Perlin noise according to Inigo Quilez. */
vec3 getHeightDerivate(in vec2 pos, in int maxLevel) {
    vec2 o = vec2(0., 0.);
    float w = 1.0;
    vec2 d = vec2(0., 0.);
    for (int i = 0; i < MAPITERATIONS; i ++) {
        vec3 n = getInterpolatedNoise(pos);
	d += n.yz;
        o += w * n.yz / (1. + dot(d, d));
        if (i >= maxLevel) {
            break;
        }
        w *= .5;
        pos *= 2.;
    }
    return vec3(o, 1.0 - sqrt(dot(o, o)));
}

int intersectTerrain(inout vec3 pos, in vec3 dir, out float t) {
    /* Dynamic LOD during marching:
     * After distance to scene has been acquired, update LOD. */
    t = 0.0;
    int maxLevel = MAPITERATIONS;
    
    for (int i = 0; i < RAYITERATIONS; i ++) {
        float height = getHeight(pos.xy, maxLevel);
        float dz = pos.z - height;

        float safeDist = dz * MARCH_SPEED;
        t += safeDist;
        pos += safeDist * dir;

	float dza = abs(dz);
        maxLevel = int(LOD / (dza * t));
        
        /* Are we close enough to terrain? */
        if (dza < STOPCONDITION * t) {
	    return maxLevel;
        }
        if (t > FOG) {
            break;
        }
    }

    t = FOG;
    return 0;
}

vec3 getTerrainColor(in vec3 pos, in int maxLevel) {
    /* Get surface normal. */
    float height = getHeight(pos.xy, maxLevel);
    vec3 d = getHeightDerivate(pos.xy, maxLevel);

    float noise = getPerlin(pos.xy * 128., 4);

    /* Sand */
    vec3 terrain = vec3(0.9, 0.9, 0.7);
	    
    /* Grass -- if terrain is not flat, the beach is shorter */
    float beachOrGrass = smoothstep(0.25, 0.26, height - d.z * .2);
    vec3 grass = vec3(0.15, 0.3, 0.0) * noise;
    terrain = mix(terrain,
		  grass,
		  beachOrGrass);

    /* Rock */
    float greenOrBarren = smoothstep(-0.2, -0.1, height - d.z + noise * .2);
    terrain = mix(terrain, vec3(0.4, 0.45, 0.5) * getInterpolatedNoise(pos.xy * 512.).x, greenOrBarren);
            
    /* Snow -- height increased if terrain is not flat. */
    float barrenOrSnow = smoothstep(1.80, 1.90, height + d.z + noise * .2);
    terrain = mix(terrain, vec3(1.2, 1.2, 1.2), barrenOrSnow);

    /* Apply lighting */
    float diffuse = (1. + smoothstep(0.3, 0.7, dot(d, SUN))) * .5;
    terrain *= diffuse;

    return terrain;
}

vec3 getSkyColor(in vec3 dir) {
    float sunAngle = pow(clamp(dot(dir, SUN), 0., 1.), 8.);
    float skyBrightness = clamp(1. - dir.z, 0., 1.);
    return vec3(skyBrightness, skyBrightness, 1.) * .8 + vec3(1., 1., 1.) * sunAngle;
}

vec3 getWaterColor(in vec3 pos, in vec3 dir) {
    /* Calculate water surface normal. */
    vec2 waves = texture2D(uWatermap, pos.xy + vec2(uTime *.07, 0.)).xy
         - texture2D(uWatermap, pos.xy + vec2(0.2, uTime *.11)).xy;
    float waterDepth = 0.4 - getHeight(pos.xy, 8);

    vec3 normal = vec3(waves, 1.0 - sqrt(dot(waves, waves)));

    /* Fresnel equation approximation */
    float reflectionOrRefraction = max(-dot(dir, normal), 0.);
    vec3 reflection = dir - 2.0 * dot(dir, normal) * normal;

    /* Reflect scenery */
    vec3 refpos = pos;
    float outLength; 
    int reflectionLevel = intersectTerrain(refpos, reflection, outLength);
    vec3 reflectedTerrain = getTerrainColor(refpos, reflectionLevel);

    /* Add sky */
    vec3 sky = getSkyColor(reflection);
    reflectedTerrain = mix(reflectedTerrain, sky, outLength/FOG);

    /* Refract scenery (utterly fake) */
    pos.xy -= waves * waterDepth;
    pos.z = getHeight(pos.xy, 8);
    vec3 refractedTerrain = getTerrainColor(pos, 8);
    refractedTerrain = mix(refractedTerrain, vec3(0., 0.2, 0.4), waterDepth);

    return mix(reflectedTerrain, refractedTerrain, reflectionOrRefraction);
}

/* Cross product. */
vec3 cross(in vec3 a, in vec3 b) {
    return vec3(
        a.y * b.z - a.z * b.y,
        a.z * b.x - a.x * b.z,
        a.x * b.y - a.y * b.x
    );
}

void main(void) {
    /* Calculate an orthonormal base for rendering. */
    vec3 left = normalize(cross(UP, uDir));
    vec3 up = cross(uDir, left);

    float groundHeight = getHeight(uPos.xy, 2);
    if (groundHeight < 0.4) {
	groundHeight = 0.4;
    }

    vec3 pos = vec3(uPos.xy, groundHeight + pow(2., -2.));
    
    vec3 dir = uDir + vPosition.x * left + vPosition.y * up;
    dir = normalize(dir);

    float distance;
    int maxLevel = intersectTerrain(pos, dir, distance);
    vec3 terrain = getTerrainColor(pos, maxLevel);

    /* Blend water if under water level. */
    if (pos.z < 0.4) {
	/* We hit a point underwater.
	 * We will now backtrack to water surface
	 * and shoot reflection and refraction rays. */

	float backtrackLength = (0.4 - pos.z) / dir.z;
	distance -= backtrackLength;
	vec3 waterPos = pos + dir * backtrackLength;

	terrain = getWaterColor(waterPos, dir);
    }

    /* Sky */
    vec3 sky = getSkyColor(dir);
    terrain = mix(terrain, sky, distance/FOG);

    gl_FragColor = vec4(sqrt(terrain), 1);
}
</script> 

<script id="shader-vs" type="x-shader/x-vertex"> 
attribute vec3 aVertexPosition;

varying vec2 vPosition;

void main(void) {
    gl_Position = vec4(aVertexPosition, 1.0);
    vPosition = gl_Position.xy;
}
</script> 

<script type="text/javascript" src="webgl.js"></script>
<script type="text/javascript">
function effectInit(gl, shader) {
    var texturesToLoad = 2;
    var textureNoise;
    var textureWater;

    var textureLoaded = function() {
        if (-- texturesToLoad != 0) {
            return;
        }
        /* Bind the noise texture to texture unit 0. */
        gl.activeTexture(gl.TEXTURE0);
        gl.bindTexture(gl.TEXTURE_2D, textureNoise);
        gl.uniform1i(gl.getUniformLocation(shader, "uNoise"), 0);

        /* Bind the water map to texture unit 1.
	 * If this doesn't work for some reason, we only
	 * lose water waves... */
        gl.activeTexture(gl.TEXTURE1);
        gl.bindTexture(gl.TEXTURE_2D, textureWater);
        gl.uniform1i(gl.getUniformLocation(shader, "uWatermap"), 1);

        effectStart(gl, shader);
    };

    textureNoise = loadTexture(gl, "noise.png", textureLoaded);
    textureWater = loadTexture(gl, "watermap.png", textureLoaded);
}
</script>
</head>
<body>

<canvas id="canvas" width="512" height="512"></canvas>

</body>
</html>
