<html><head><title>Landscape rendering experiment</title><script id="shader-fs" type="x-shader/x-fragment"> 
#ifdef GL_ES
precision highp float;
#endif

/* System's clock, for running animations. */
uniform float uTime;
/* Position of camera. */
uniform vec3 uPos;
/* Lookat of camera. */
uniform vec3 uDir;
/* Water texture */
uniform sampler2D uWatermap;
/* Noise texture */
uniform sampler2D uNoise;

/* Coordinates being currently rendered. */
varying vec2 vPosition;

/* How far to subdivide modified Perlin noise.
 * (Determines how coarse the terrain is.) */
const int MAPITERATIONS = 8;
/* How many march iterations per ray. */
const int RAYITERATIONS = 256;
/* Convert distance between ray and terrain position to "safe" advancing step. */
const float MARCH_SPEED = 0.4;
/* Level Of Detail: how far to render until dropping terrain features. */
const float LOD = 2.0;
/* When ray reaches close enough to terrain, stop marching */
const float STOPCONDITION = 0.0025;
/* Render distance. */
const float FOG = 5.;
/* Water level */
const float WATER_LEVEL = 0.4;
/* Direction to infinitely far-away Sun */
const vec3 SUN = vec3(0.8, 0., 0.6);
/* The camera up direction. */
const vec3 UP = vec3(0., 0., 1.);

/* Substitute with white noise generator. */
float getNoise(in float x, in float y) {
    return texture2D(uNoise, vec2(x, y)).g;
}

/* Derivation by Inigo Quilez. */
vec3 getInterpolatedNoise(in vec2 pos) {
    float i = floor(pos.x) / 256.;
    float j = floor(pos.y) / 256.;
    float u = fract(pos.x);
    float v = fract(pos.y);

    float du = -30.0*u*u*(u*(u-2.0)+1.0);
    float dv = -30.0*v*v*(v*(v-2.0)+1.0);
    u = u*u*u*(u*(u*6.0-15.0)+10.0);
    v = v*v*v*(v*(v*6.0-15.0)+10.0);

    float a = getNoise(i, j);
    float b = getNoise(i+1./256., j);
    float c = getNoise(i, j+1./256.);
    float d = getNoise(i+1./256., j+1./256.);

    float k0 = a;
    float k1 = b - a;
    float k2 = c - a;
    float k3 = d - c - k1;

    return vec3(
        k0 + k1*u + k2*v + k3*u*v,
        du * (k1 + k3*v),
        dv * (k2 + k3*u)
    );
}

/* Generate Perlin noise to given octave. */
float getPerlin(in vec2 pos, in int maxLevel) {
    float o = 0.;
    float w = 0.5;
    for (int i = 0; i < MAPITERATIONS; i ++) {
        vec3 n = getInterpolatedNoise(pos);
        o += w * n.x;
        if (i >= maxLevel) {
            break;
        }
        w *= .5;
        pos *= 2.;
    }
    return o;
}

/* Modified Perlin noise according to Inigo Quilez. */
float getHeight(in vec2 pos, in int maxLevel) {
    float o = 0.;
    float w = 1.0;
    vec2 d = vec2(0., 0.);
    for (int i = 0; i < MAPITERATIONS; i ++) {
        vec3 n = getInterpolatedNoise(pos);
	d += n.yz;
        o += w * n.x / (1. + dot(d, d) * .25);
        if (i >= maxLevel) {
            break;
        }
        w *= .5;
        pos *= 2.;
    }
    return o;
}

/* Modified Perlin noise according to Inigo Quilez. */
vec3 getHeightDerivate(in vec2 pos, in int maxLevel) {
    vec2 o = vec2(0., 0.);
    float w = 1.0;
    vec2 d = vec2(0., 0.);
    for (int i = 0; i < MAPITERATIONS; i ++) {
        vec3 n = getInterpolatedNoise(pos);
	d += n.yz;
        o += w * n.yz / (1. + dot(d, d) * .25);
        if (i >= maxLevel) {
            break;
        }
        w *= .5;
        pos *= 2.;
    }
    return vec3(o, 1.0 - sqrt(dot(o, o)));
}

/* Terrain marching.
 *
 * At each step along the way, the height of the ray from
 * ground is computed, and this is converted into a guesstimate
 * about a "safe" distance for ray to skip forwards.
 *
 * The octaves of Perlin noise that generates the landscape are
 * reduced if the ray appears to be far from the terrain, or
 * far from viewer.
 *
 * The stop condition also gets progressively coarser as
 * the ray gains distance to viewer. This should naturally "antialias"
 * the scenery at distance.
 */
int intersectTerrain(inout vec3 pos, in vec3 dir, out float t) {
    /* Dynamic LOD during marching:
     * After distance to scene has been acquired, update LOD. */
    t = 0.0;
    int maxLevel = MAPITERATIONS;
    
    for (int i = 0; i < RAYITERATIONS; i ++) {
        float height = pos.z - getHeight(pos.xy, maxLevel);

	/* There's a trick here. Our distance estimates are not conservative;
	 * if the ray steps into the terrain, height is negative, and the ray will
         * backtrack automatically. */
        float distanceEstimate = height * MARCH_SPEED;
        t += distanceEstimate;
        pos += distanceEstimate * dir;

	/* Dynamic level of detail. */
	float absHeight = abs(height);
        maxLevel = int(LOD / (absHeight * t));
        
        /* Are we close enough to terrain? */
        if (absHeight < STOPCONDITION * t) {
	    return maxLevel;
        }

        if (t > FOG) {
            break;
        }
    }

    t = FOG;
    return 0;
}

vec3 getTerrainColor(in vec3 pos, in int maxLevel) {
    /* Get surface normal. */
    vec3 d = getHeightDerivate(pos.xy, maxLevel);

    float noise = getPerlin(pos.xy * 128., 4);

    /* Sand */
    vec3 terrain = vec3(0.9, 0.9, 0.7);
	    
    /* Grass -- if terrain is not flat, the beach is shorter */
    float beachOrGrass = smoothstep(0.25, 0.26, pos.z - d.z * .2);
    vec3 grass = vec3(0.15, 0.3, 0.0) * noise;
    terrain = mix(terrain,
		  grass,
		  beachOrGrass);

    /* Rock */
    float greenOrBarren = smoothstep(-0.2, -0.1, pos.z - d.z + noise * .2);
    terrain = mix(terrain, vec3(0.4, 0.45, 0.5) * getInterpolatedNoise(pos.xy * 512.).x, greenOrBarren);
            
    /* Snow -- height increased if terrain is not flat. */
    float barrenOrSnow = smoothstep(1.80, 1.90, pos.z + d.z + noise * .2);
    terrain = mix(terrain, vec3(1.4, 1.4, 1.4), barrenOrSnow);

    /* Apply lighting */
    float diffuse = max(dot(d, SUN), 0.);
    terrain *= diffuse;

    return terrain;
}

/* Render sky. */
vec3 getSkyColor(in vec3 pos, in vec3 dir) {
    float sunSpecular = pow(max(dot(dir, SUN), 0.), 40.);
    float skyBrightness = clamp(1. - dir.z, 0., 1.);
    float clouds = smoothstep(0.2, 0.9, getPerlin(pos.xy + dir.xy * 8. / dir.z, 4));
    return mix(vec3(skyBrightness, skyBrightness, 1.) * .8, vec3(1., 1., 1.), clouds)
	+ vec3(2., 2., 2.) * sunSpecular;
}

/* Calculate water color based on reflection and refraction. */
vec3 getWaterColor(in vec3 pos, in vec3 dir) {
    /* Calculate water surface normal. */
    vec2 wave1 = texture2D(uWatermap, pos.xy * 1.7 + vec2(uTime *.07, 0.)).xy;
    vec2 wave2 = texture2D(uWatermap, pos.xy + vec2(0., uTime *.11)).xy;
    vec2 waves = wave1 - wave2;
    vec3 normal = vec3(waves, 1.0 - sqrt(dot(waves, waves)));

    /* Fresnel equation approximation */
    float reflectionOrRefraction = max(-dot(dir, normal), 0.);
    reflectionOrRefraction = sqrt(reflectionOrRefraction);
    vec3 reflection = dir - 2. * normal * dot(dir, normal);

    /* Reflect scenery */
    vec3 refpos = pos;
    float outLength; 
    int reflectionLevel = intersectTerrain(refpos, reflection, outLength);
    vec3 reflectedTerrain = getTerrainColor(refpos, reflectionLevel);

    /* Add sky */
    vec3 sky = getSkyColor(pos, reflection);
    reflectedTerrain = mix(reflectedTerrain, sky, outLength/FOG);

    /* Refract scenery (utterly fake). */
    float waterDepth = WATER_LEVEL - getHeight(pos.xy, 4);
    pos.xy -= waves * waterDepth;
    pos.z = getHeight(pos.xy, 4);
    vec3 refractedTerrain = getTerrainColor(pos, 8);
    /* caustics */
    refractedTerrain *= 1. + waterDepth * pow(.6 + .5 * sin((waterDepth + wave1.x + wave2.x) * 30.), 8.);

    return mix(reflectedTerrain, refractedTerrain, reflectionOrRefraction);
}

/* Cross product. */
vec3 cross(in vec3 a, in vec3 b) {
    return vec3(
        a.y * b.z - a.z * b.y,
        a.z * b.x - a.x * b.z,
        a.x * b.y - a.y * b.x
    );
}

void main(void) {
    /* Calculate position of camera. */
    float groundHeight = getHeight(uPos.xy, 2);
    if (groundHeight < WATER_LEVEL) {
	groundHeight = WATER_LEVEL;
    }
    vec3 cameraPos = vec3(uPos.xy, groundHeight + pow(2., -2.));
    
    /* Calculate an orthonormal basis based on lookat and up vector. */
    vec3 left = normalize(cross(UP, uDir));
    vec3 up = cross(uDir, left);
    vec3 dir = uDir + vPosition.x * left + vPosition.y * up;
    dir = normalize(dir);
    
    /* Terrain march. */
    float distance;
    vec3 terrainPos = cameraPos;
    int maxLevel = intersectTerrain(terrainPos, dir, distance);

    /* Blend water if under water level. */
    vec3 terrain;
    if (terrainPos.z < WATER_LEVEL) {
	/* Backtrack to water surface. */
	float backtrackLength = (WATER_LEVEL - terrainPos.z) / dir.z;
	distance -= backtrackLength;
	vec3 waterPos = terrainPos + dir * backtrackLength;
	terrain = getWaterColor(waterPos, dir);
    } else {
	terrain = getTerrainColor(terrainPos, maxLevel);
    }

    /* Distance fog. */
    terrain = mix(terrain, vec3(.8, .8, .8), distance/FOG);

    /* Sky */
    if (distance / FOG == 1.) {
	terrain = getSkyColor(cameraPos, dir);
    }

    float exposureControl = clamp(dot(uDir, SUN), 0., 1.);
    gl_FragColor = vec4(sqrt(terrain / (1. + exposureControl)), 1);
}
</script> 

<script id="shader-vs" type="x-shader/x-vertex"> 
attribute vec3 aVertexPosition;

varying vec2 vPosition;

void main(void) {
    gl_Position = vec4(aVertexPosition, 1.0);
    vPosition = gl_Position.xy;
}
</script> 

<script type="text/javascript" src="webgl.js"></script>
<script type="text/javascript">
function effectInit(gl, shader) {
    var texturesToLoad = 2;
    var textureNoise;
    var textureWater;

    var textureLoaded = function() {
        if (-- texturesToLoad != 0) {
            return;
        }
        /* Bind the noise texture to texture unit 0. */
        gl.activeTexture(gl.TEXTURE0);
        gl.bindTexture(gl.TEXTURE_2D, textureNoise);
        gl.uniform1i(gl.getUniformLocation(shader, "uNoise"), 0);

        /* Bind the water map to texture unit 1.
	 * If this doesn't work for some reason, we only
	 * lose water waves... */
        gl.activeTexture(gl.TEXTURE1);
        gl.bindTexture(gl.TEXTURE_2D, textureWater);
        gl.uniform1i(gl.getUniformLocation(shader, "uWatermap"), 1);

        effectStart(gl, shader);
    };

    textureNoise = loadTexture(gl, "noise.png", textureLoaded);
    textureWater = loadTexture(gl, "watermap.png", textureLoaded);
}
</script>
</head>
<body>

<canvas id="canvas" width="512" height="512"></canvas>

</body>
</html>
